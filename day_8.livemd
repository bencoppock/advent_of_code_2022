# Day 8: Treetop Tree House

```elixir
Mix.install([
  {:kino, "~> 0.7.0"}
])
```

## Part 1

The expedition comes across a peculiar patch of tall trees all planted carefully in a grid. The Elves explain that a previous expedition planted these trees as a reforestation effort. Now, they're curious if this would be a good location for a tree house.

First, determine whether there is enough tree cover here to keep a tree house **hidden**. To do this, you need to count the number of trees that are **visible from outside the grid** when looking directly along a row or column.

The Elves have already launched a quadcopter to generate a map with the height of each tree (your puzzle input). For example:

```
30373
25512
65332
33549
35390
```

Each tree is represented as a single digit whose value is its height, where `0` is the shortest and `9` is the tallest.

A tree is **visible** if all of the other trees between it and an edge of the grid are **shorter** than it. Only consider trees in the same row or column; that is, only look up, down, left, or right from any given tree.

All of the trees around the edge of the grid are **visible** - since they are already on the edge, there are no trees to block the view. In this example, that only leaves the **interior nine trees** to consider:

* The top-left `5` is **visible** from the left and top. (It isn't visible from the right or bottom since other trees of height `5` are in the way.)
* The top-middle `5` is **visible** from the top and right.
* The top-right `1` is not visible from any direction; for it to be visible, there would need to only be trees of height `0` between it and an edge.
* The left-middle `5` is **visible**, but only from the right.
* The center `3` is not visible from any direction; for it to be visible, there would need to be only trees of at most height `2` between it and an edge.
* The right-middle `3` is **visible** from the right.
* In the bottom row, the middle `5` is **visible**, but the `3` and `4` are not.

With 16 trees visible on the edge and another 5 visible in the interior, a total of `21` trees are visible in this arrangement.

Consider your map; **how many trees are visible from outside the grid?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(autorun: false)

defmodule TreeVisibilityTest do
  use ExUnit.Case

  test "mark_row_visibility" do
    assert [{3, true}, {0, false}, {3, false}, {7, true}, {3, true}] =
             Trees.mark_row_visibility([{3, nil}, {0, nil}, {3, nil}, {7, nil}, {3, nil}])

    assert [{2, true}, {5, true}, {5, true}, {1, false}, {2, true}] =
             Trees.mark_row_visibility([{2, nil}, {5, nil}, {5, nil}, {1, nil}, {2, nil}])

    assert [{6, true}, {5, true}, {3, false}, {3, true}, {2, true}] =
             Trees.mark_row_visibility([{6, nil}, {5, nil}, {3, nil}, {3, nil}, {2, nil}])

    assert [{3, true}, {3, false}, {5, true}, {4, false}, {9, true}] =
             Trees.mark_row_visibility([{3, nil}, {3, nil}, {5, nil}, {4, nil}, {9, nil}])

    assert [{3, true}, {5, true}, {3, false}, {9, true}, {0, true}] =
             Trees.mark_row_visibility([{3, nil}, {5, nil}, {3, nil}, {9, nil}, {0, nil}])
  end

  test "transpose" do
    assert [[1, 4, 7], [2, 5, 8], [3, 6, 9]] = Trees.transpose([[1, 2, 3], [4, 5, 6], [7, 8, 9]])
  end
end

ExUnit.run()
```

<!-- livebook:{"disable_formatting":true} -->

```elixir
input = Kino.Input.textarea("Data Input:")
```

```elixir
defmodule TreeVisibility do
  def mark_visibility(grid) do
    grid
    |> mark_horizontal_visibility()
    |> transpose()
    |> mark_horizontal_visibility()
    |> transpose()
  end

  defp mark_horizontal_visibility(grid) do
    for row <- grid, do: mark_row_visibility(row)
  end

  def mark_row_visibility(row) do
    row
    |> mark_forward_visibility()
    |> Enum.reverse()
    |> mark_forward_visibility()
    |> Enum.reverse()
  end

  defp mark_forward_visibility(row) do
    {marked_row, _max_height} =
      Enum.map_reduce(row, nil, fn {height, is_visible}, max_height ->
        case max_height do
          nil -> {{height, true}, height}
          max when height > max -> {{height, true}, height}
          _ -> {{height, is_visible || false}, max_height}
        end
      end)

    marked_row
  end

  def transpose(grid) do
    grid
    |> Enum.zip()
    |> Enum.map(&Tuple.to_list/1)
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input
|> Kino.Input.read()
|> String.split()
|> Enum.map(fn row ->
  row
  |> String.splitter("", trim: true)
  |> Enum.map(fn i -> {String.to_integer(i), nil} end)
end)
# |> dbg
|> TreeVisibility.mark_visibility()
# |> dbg 
|> Enum.flat_map(fn row ->
  Enum.map(row, fn {_height, is_visible} -> is_visible end)
end)
|> Enum.count(& &1)
```

## Part 2

Content with the amount of tree cover available, the Elves just need to know the best spot to build their tree house: they would like to be able to see a lot of **trees**.

To measure the viewing distance from a given tree, look up, down, left, and right from that tree; stop if you reach an edge or at the first tree that is the same height or taller than the tree under consideration. (If a tree is right on the edge, at least one of its viewing distances will be zero.)

The Elves don't care about distant trees taller than those found by the rules above; the proposed tree house has large eaves to keep it dry, so they wouldn't be able to see higher than the tree house anyway.

In the example above, consider the middle 5 in the second row:

```
30373
25512
65332
33549
35390
```

* Looking up, its view is not blocked; it can see **`1`** tree (of height `3`).
* Looking left, its view is blocked immediately; it can see only **`1`** tree (of height `5`, right next to it).
* Looking right, its view is not blocked; it can see **`2`** trees.
* Looking down, its view is blocked eventually; it can see **`2`** trees (one of height `3`, then the tree of height `5` that blocks its view).

A tree's scenic score is found by multiplying together its viewing distance in each of the four directions. For this tree, this is 4 (found by multiplying 1 * 1 * 2 * 2).

However, you can do even better: consider the tree of height 5 in the middle of the fourth row:

```
30373
25512
65332
33549
35390
```

* Looking up, its view is blocked at **`2`** trees (by another tree with a height of `5`).
* Looking left, its view is not blocked; it can see **`2`** trees.
* Looking down, its view is also not blocked; it can see **`1`** tree.
* Looking right, its view is blocked at **`2`** trees (by a massive tree of height `9`).

This tree's scenic score is **`8`** (2 * 2 * 1 * 2); this is the ideal spot for the tree house.

Consider each tree on your map. **What is the highest scenic score possible for any tree?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
ExUnit.start(autorun: false)

defmodule ScenicVisibilityTest do
  use ExUnit.Case

  test "distance" do
    assert 2 = ScenicVisibility.distance({3, []}, [{0, []}, {3, []}, {7, []}, {3, []}])
    assert 1 = ScenicVisibility.distance({0, []}, [{3, []}, {7, []}, {3, []}])
    assert 1 = ScenicVisibility.distance({3, []}, [{7, []}, {3, []}])
    assert 1 = ScenicVisibility.distance({7, []}, [{3, []}])
    assert 0 = ScenicVisibility.distance({3, []}, [])
  end

  test "mark_row_visibility" do
    assert [{3, [0, 2]}, {0, [1, 1]}, {3, [2, 1]}, {7, [3, 1]}, {3, [1, 0]}] =
             ScenicVisibility.mark_row_visibility(ScenicVisibility.prep_row([3, 0, 3, 7, 3]))

    assert [{0, [0, 1]}, {5, [1, 1]}, {5, [1, 2]}, {3, [1, 1]}, {5, [2, 0]}] =
             ScenicVisibility.mark_row_visibility(ScenicVisibility.prep_row([0, 5, 5, 3, 5]))
  end

  test "highest_scenic_score" do
    input = """
    30373
    25512
    65332
    33549
    35390\
    """

    assert 8 =
             input
             |> String.split()
             |> Enum.map(fn row ->
               row
               |> String.splitter("", trim: true)
               |> Enum.map(&String.to_integer/1)
             end)
             |> ScenicVisibility.highest_scenic_score()
  end
end

ExUnit.run()
```

```elixir
defmodule ScenicVisibility do
  def prep_grid(grid), do: Enum.map(grid, &prep_row/1)

  def prep_row(row) do
    Enum.map(row, fn height -> {height, []} end)
  end

  defp mark_viewing_distances(prepped_grid) do
    prepped_grid
    |> mark_horizontal_visibility()
    |> transpose()
    |> mark_horizontal_visibility()
    |> transpose()
  end

  defp mark_horizontal_visibility(prepped_grid) do
    for row <- prepped_grid, do: mark_row_visibility(row)
  end

  def mark_row_visibility(prepped_row, pass \\ :first, marked \\ [])
  def mark_row_visibility([], :first, marked), do: mark_row_visibility(marked, :second)
  def mark_row_visibility([], :second, marked), do: marked

  def mark_row_visibility([first | rest], pass, marked) do
    {height, scores} = first
    tree = {height, [distance(first, rest) | scores]}
    mark_row_visibility(rest, pass, [tree | marked])
  end

  def distance({height, _}, rest) do
    Enum.reduce_while(rest, 0, fn
      {next_height, _}, acc when height > next_height -> {:cont, acc + 1}
      _, acc -> {:halt, acc + 1}
    end)
  end

  def transpose(grid) do
    grid
    |> Enum.zip()
    |> Enum.map(&Tuple.to_list/1)
  end

  def highest_scenic_score(grid) do
    grid
    |> prep_grid()
    |> mark_viewing_distances()
    |> Enum.flat_map(fn marked_row ->
      for {_height, distances} <- marked_row do
        Enum.reduce(distances, 1, &*/2)
      end
    end)
    |> Enum.max()
  end
end
```

```elixir
grid =
  input
  |> Kino.Input.read()
  |> String.split()
  |> Enum.map(fn row ->
    row
    |> String.splitter("", trim: true)
    |> Enum.map(&String.to_integer/1)
  end)

grid
|> ScenicVisibility.highest_scenic_score()
```
