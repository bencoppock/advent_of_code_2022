# Day 5: Supply Stacks

```elixir
Mix.install([
  {:kino, "~> 0.7.0"}
])
```

## Part 1

The expedition can depart as soon as the final supplies have been unloaded from the ships. Supplies are stored in stacks of marked crates, but because the needed supplies are buried under many other crates, the crates need to be rearranged.

The ship has a giant cargo crane capable of moving crates between stacks. To ensure none of the crates get crushed or fall over, the crane operator will rearrange them in a series of carefully-planned steps. After the crates are rearranged, the desired crates will be at the top of each stack.

The Elves don't want to interrupt the crane operator during this delicate procedure, but they forgot to ask her which crate will end up where, and they want to be ready to unload them as soon as possible so they can embark.

They do, however, have a drawing of the starting stacks of crates and the rearrangement procedure (your puzzle input). For example:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
In this example, there are three stacks of crates. Stack 1 contains two crates: crate Z is on the bottom, and crate N is on top. Stack 2 contains three crates; from bottom to top, they are crates M, C, and D. Finally, stack 3 contains a single crate, P.

Then, the rearrangement procedure is given. In each step of the procedure, a quantity of crates is moved from one stack to a different stack. In the first step of the above rearrangement procedure, one crate is moved from stack 2 to stack 1, resulting in this configuration:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

In the second step, three crates are moved from stack 1 to stack 3. Crates are moved one at a time, so the first crate to be moved (D) ends up below the second and third crates:

```
        [Z]
        [N]
    [C] [D]
    [M] [P]
 1   2   3
```

Then, both crates are moved from stack 2 to stack 1. Again, because crates are moved one at a time, crate C ends up below crate M:

```
        [Z]
        [N]
[M]     [D]
[C]     [P]
 1   2   3
```

Finally, one crate is moved from stack 1 to stack 2:

```
        [Z]
        [N]
        [D]
[C] [M] [P]
 1   2   3
```

The Elves just need to know which crate will end up on top of each stack; in this example, the top crates are C in stack 1, M in stack 2, and Z in stack 3, so you should combine these together and give the Elves the message CMZ.

**After the rearrangement procedure completes, what crate ends up on top of each stack?**

<!-- livebook:{"disable_formatting":true} -->

```elixir
input = Kino.Input.textarea("Data Input:")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Stacks do
  def parse_block(diagram) when is_binary(diagram) do
    [_column_labels | reversed_crate_rows] =
      diagram
      |> String.split("\n")
      |> Enum.reverse()

    reversed_crate_rows
    |> Enum.map(&parse_row/1)
    |> Enum.reduce(%{}, &build_row/2)
  end

  defp parse_row(row, items \\ []) when is_binary(row) do
    case row do
      "[" <> <<char>> <> "] " <> rest -> parse_row(rest, [char | items])
      "    " <> rest -> parse_row(rest, [nil | items])
      "[" <> <<char>> <> "]" -> finalize([char | items])
      "   " -> finalize([nil | items])
    end
  end

  defp finalize(items), do: Enum.reverse(items)

  defp build_row(row, stacks) when is_list(row) and is_map(stacks) do
    row
    |> Enum.with_index(1)
    |> Enum.reduce(stacks, &add_crate/2)
  end

  defp add_crate({nil, _column}, stacks), do: stacks

  defp add_crate({crate, column}, stacks) do
    Map.update(stacks, column, [crate], fn stack -> [crate | stack] end)
  end

  def top_crates(stacks) do
    Enum.map(stacks, fn {_label, [top | _rest]} -> top end)
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Instructions do
  defstruct [:move, :from, :to]

  def parse_block(instructions) when is_binary(instructions) do
    instructions
    |> String.split("\n")
    |> Enum.map(&parse_instruction/1)
  end

  @pattern ~r/^move (\d+) from (\d+) to (\d+)$/

  defp parse_instruction(instruction) do
    [move, from, to] = Regex.run(@pattern, instruction, capture: :all_but_first)
    struct!(__MODULE__, move: to_int(move), from: to_int(from), to: to_int(to))
  end

  defp to_int(char), do: char |> String.to_integer()

  def apply_all(stacks, instructions, version \\ :v9000) do
    Enum.reduce(instructions, stacks, &apply_one(&2, &1, version))
  end

  defp apply_one(stacks, %__MODULE__{move: move, from: from, to: to}, :v9000) do
    1..move
    |> Enum.reduce(stacks, fn _i, acc ->
      [moving | remaining] = Map.fetch!(acc, from)

      acc
      |> Map.put(from, remaining)
      |> Map.update!(to, fn existing -> [moving | existing] end)
    end)
  end

  defp apply_one(stacks, %__MODULE__{move: move, from: from, to: to}, :v9001) do
    stack = Map.fetch!(stacks, from)
    {moving, remaining} = Enum.split(stack, move)

    stacks
    |> Map.put(from, remaining)
    |> Map.update!(to, fn existing -> moving ++ existing end)
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
[stacks_block, instructions_block] =
  input
  |> Kino.Input.read()
  |> String.split("\n\n")

stacks = Stacks.parse_block(stacks_block) |> dbg
instructions = Instructions.parse_block(instructions_block)
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
stacks
|> Instructions.apply_all(instructions, :v9000)
|> Stacks.top_crates()
```

## Part 2

As you watch the crane operator expertly rearrange the crates, you notice the process isn't following your prediction.

Some mud was covering the writing on the side of the crane, and you quickly wipe it away. The crane isn't a CrateMover 9000 - it's a CrateMover 9001.

The CrateMover 9001 is notable for many new and exciting features: air conditioning, leather seats, an extra cup holder, and the ability to pick up and move multiple crates at once.

Again considering the example above, the crates begin in the same configuration:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

Moving a single crate from stack 2 to stack 1 behaves the same as before:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

However, the action of moving three crates from stack 1 to stack 3 means that those three moved crates stay in the same order, resulting in this new configuration:

```
        [D]
        [N]
    [C] [Z]
    [M] [P]
 1   2   3
```

Next, as both crates are moved from stack 2 to stack 1, they retain their order as well:

```
        [D]
        [N]
[C]     [Z]
[M]     [P]
 1   2   3
```

Finally, a single crate is still moved from stack 1 to stack 2, but now it's crate C that gets moved:

```
        [D]
        [N]
        [Z]
[M] [C] [P]
 1   2   3
```

In this example, the CrateMover 9001 has put the crates in a totally different order: MCD.

Before the rearrangement process finishes, update your simulation so that the Elves know where they should stand to be ready to unload the final supplies. **After the rearrangement procedure completes, what crate ends up on top of each stack?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
stacks
|> Instructions.apply_all(instructions, :v9001)
|> Stacks.top_crates()
```
